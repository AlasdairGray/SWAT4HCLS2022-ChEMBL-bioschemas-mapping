PREFIX chembl: <http://rdf.ebi.ac.uk/terms/chembl#>
PREFIX cheminf: <http://semanticscience.org/resource/>
PREFIX chembl_mol: <http://rdf.ebi.ac.uk/resource/chembl/molecule/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>

CONSTRUCT 
{
  ?s a schema:MolecularEntity.
  ?s schema:inChI ?inchi.
  ?s schema:inChIKey ?inchiKey.
  ?s schema:molecularFormula ?molFormula.
  ?s schema:molecularWeight ?molWeight.
  ?s schema:smiles ?smiles.
  ?s schema:monoisotopicMolecularWeight ?monoisoWeight.
}
WHERE
{
  VALUES ?s { @_MOLS_@ }
  
  ?s a chembl:SmallMolecule.

  OPTIONAL{
	?s cheminf:SIO_000008 [ a cheminf:CHEMINF_000113;
							cheminf:SIO_000300 ?inchi].  
  }
 
  OPTIONAL{ 
	?s cheminf:SIO_000008 [ a cheminf:CHEMINF_000059; 
							cheminf:SIO_000300 ?inchiKey].
  }
  
  OPTIONAL{
	?s cheminf:SIO_000008 [ a cheminf:CHEMINF_000042; 
							cheminf:SIO_000300 ?molFormula].
  }
  
  OPTIONAL{
	?s cheminf:SIO_000008 [ a cheminf:CHEMINF_000216; 
							cheminf:SIO_000300 ?molWeight].
  }
  
  OPTIONAL{
	?s cheminf:SIO_000008 [ a cheminf:CHEMINF_000218; 
							cheminf:SIO_000300 ?monoisoWeight].
  }
  
  OPTIONAL{
	?s cheminf:SIO_000008 [ a cheminf:CHEMINF_000018; 
							cheminf:SIO_000300 ?smiles].
  }
} 